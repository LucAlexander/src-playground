bind 1 +comment:// ... $ {
	
}

bind 1 hoist_anchor {
	[ mov
	| movh
	| movl
	| add
	| sub
	| mul
	| div
	| mod
	| and
	| or
	| xor
	| not
	| com
	| int
	| jmp
	| cmp
	| jle
	| jge
	| jlt
	| jgt
	| jeq
	| jne
	| shl
	| shr
	| bind
	| explicit_hoist_anchor
	]
}

bind 1 label +name {
	bind 0 name = ip
}

bind 1 let +name +binding:\= ... in +body:\{ ... \} {
	comptime {
		body
	}
	bind 0 name = binding
}

bind 1 comptime +body:\{ ... \} {
	comp
		explicit_hoist_anchor
		body
		mov r0 !1
		int
	run
	explicit_hoist_anchor
}

comptime{
	let tmp = r1 in {
		mov r1 !mtp
		sub r1 r1 !8
	}
	let sp = r1 in { sub r1 r1 !8 }
	let fp = r1 in { sub r1 r1 !8 }
	mov sp !mtp
	sub sp sp !10
	mov fp sp
}

bind 0 tmp = !tmp

bind 0 sp = !sp
bind 0 fp = !fp
mov sp !mtp
sub sp sp !10
mov fp sp

bind 1 +x integer {
	sub sp sp !8
	mov [sp] !x
}

bind 1 , +x {
	sub sp sp !8
	mov [sp] x
}

bind 1 pop +r {
	mov r [sp]
	add sp sp !8
}

bind 1 stderr {
	mov r0 !2
	pop r1
	int
	,r1
}

bind 1 \: +word +body:% ... \; {
	bind 1 word {
		body
	}
}

// primitives

bind 1 \+ {
	pop r0
	pop r1
	add r0 r0 r1
	,r0
}

bind 1 \- {
	pop r0
	pop r1
	sub r0 r1 r0
	,r0
}

bind 1 * {
	pop r0
	pop r1
	mul r0 r0 r1
	,r0
}

bind 1 end {
	mov r0 !1
	int
}

bind 1 explicit_hoist_anchor {
}

bind 1 dup {
	pop r0
	,r0 ,r0
}

bind 1 ovr {
	pop r0
	pop r1
	,r1 ,r0 ,r1
}

bind 1 swp {
	pop r0
	pop r1
	,r0 ,r1
}

// test

:square dup * ;

:cube dup dup * * ;

2 integer square cube stderr end

